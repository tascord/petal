// monads
negate = { "!" }
negative = { "-" }

// dyads
pow = { "**" }
equality = { "==" }
add = { "+" }
subtract = { "-" }
multiply = { "*" }
divide = { "/" }
or = { "||" }
and = { "&&" }
gt = { ">" }
lt = { "<" }
gte = { ">=" }
lte = { "<=" }

// delimiters
lparen = _{ "(" }
rparen = _{ ")" }
lbrace = _{ "{" }
rbrace = _{ "}" }
comma = _{ "," }
colon = _{ ":" }
semicolon = _{ ";" }
assignment = _{ "=" }

// keywords
declaration = { "let" }
visibility = { "pub" | "local" }
function = { "fn" }
return = { "ret" }
slf = { "self" }
ret = _{ "return" }
if = _{ "if" }
else = _{ "else" }
while = _{ "while" }

// literals
boolean = { "true" | "false" }
float = { ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+ }
int = { ASCII_DIGIT+ }
null = { "null" }

// strings
string = ${ "\"" ~ inner ~ "\"" }
inner = _{ char* }
char = _{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

// identifiers
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

/* ------------------------ */

array = { "[" ~ (expr | term) ~ ("," ~ (expr | term))* ~ "]" }

literal = _{ float | int | string | boolean | array | null }
term = _{ literal | identifier | lparen ~ expr ~ rparen }
terms = { term+ }

// shorthand
typed_var = { (identifier ~ typed) | slf }
named_var = _{ identifier ~ colon ~ expr }
typed_args = { typed_var ~ (comma ~ typed_var)* }
args = _{ (expr | identifier) ~ (comma ~ (expr | identifier))* }
typed = { (colon ~ (identifier)) | slf }

// expressions
monadic = { (negative | negate) ~ term }
dyadic = { term ~ ((pow | equality | add | subtract | multiply | divide | or | and | gt | lt | gte | lte) ~ term)+ }
expr = { fn_call | struct_inst | var_decl | var_assign | monadic | dyadic | index | terms | lambda }

// block scopes
block = { lbrace ~ thing* ~ rbrace }

// functions
fn_outline = { visibility? ~ function ~ identifier ~ lparen ~ typed_args? ~ rparen ~ typed? }
fn_decl = { fn_outline ~ block }
fn_call = { identifier ~ lparen ~ args? ~ rparen }

// variables
var_decl = { visibility? ~ declaration ~ identifier ~ typed? ~ assignment ~ expr }
var_assign = { identifier ~ assignment ~ expr }

// indexing
index = { term ~ (("." ~ expr) | ("[" ~ expr ~ "]"))+ }

// struct
struct_inner = { lbrace ~ (named_var ~ (comma ~ named_var)*)* ~ rbrace }
struct_inst = { identifier ~ lbrace ~ (named_var ~ (comma ~ named_var)*)* ~ rbrace }

// conditionals
if_arm = { if ~ expr ~ block }
else_if_arm = { else ~ if ~ expr ~ block }
else_arm = { else ~ block }
conditional = { if_arm ~ else_if_arm* ~ else_arm? }

// loops
loop_while = { while ~ expr ~ block }

// lambda
lambda = { "|" ~ args? ~ "|" ~ typed? ~ block }

// return
ret_stmt = { ret ~ expr }

// ltls
ltl = { fn_decl | ret_stmt | conditional | loop_while }

thing = _{ (ltl | (expr ~ semicolon)) }
program = _{ SOI ~ thing+ ~ EOI? }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }